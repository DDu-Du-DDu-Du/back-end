plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
    id 'checkstyle'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.devcourse'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

jacoco {
    toolVersion = '0.8.11'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    // SpringBoot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-jose'

    // MySql
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // Faker
    implementation 'net.datafaker:datafaker:2.0.2'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // RestDocs
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

def querydslSrcDir = 'src/main/generated'

clean {
    delete file(querydslSrcDir)
}

processResources.dependsOn('copyMainSecret')
processTestResources.dependsOn('copyTestSecret')

tasks.register('copyMainSecret', Copy) {
    from './secrets/main'
    include 'application*.yaml'
    into './src/main/resources'
}

tasks.register('copyTestSecret', Copy) {
    from './secrets/test'
    include 'application*.yaml'
    into './src/test/resources'
}

tasks.withType(JavaCompile) {
    options.generatedSourceOutputDirectory = file(querydslSrcDir)
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.named('test') {
    useJUnitPlatform()

    finalizedBy jacocoTestReport
    outputs.dir snippetsDir
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
        csv.required = false

        html.outputLocation = layout.buildDirectory.dir('jacocoReports/html')
        xml.outputLocation = layout.buildDirectory.file('jacocoReports/jacoco.xml')
    }

    excludedClassFilesForJacoco(classDirectories)

    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    excludedClassFilesForJacoco(classDirectories)

    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.60
            }

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.70
            }

            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 200
            }
        }
    }
}

private excludedClassFilesForJacoco(classDirectories) {
    classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/*Application*",
                        "**/*Config*",
                        "**/*Q*"
                ])
            })
    )
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    sources {
        include 'index.adoc'
    }
    dependsOn test
    outputDir 'src/docs/asciidoc'
}

bootJar {
    dependsOn asciidoctor
    copy {
        from "${asciidoctor.outputDir}"
        into 'build/static/docs'
    }
}
